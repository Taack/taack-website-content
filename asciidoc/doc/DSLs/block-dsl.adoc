= Block DSL
:doctype: book
:taack-category: 10|doc/DSLs
:toc:
:source-highlighter: rouge

[%interactive]
* [*] Rendered into HTML
* [*] Rendered into PDF
* [ ] Rendered into CSV

Top level DSL that is responsible for arranging page graphical elements and update ajaxBlocks altogether. In case we are rendering a complete page, we also have to specify the page menu description via the link:menu-dsl.adoc[Menu DSL].

This DSL is also responsible for managing modal windows stack. Open a modal using `modal` or close modal using `closeModalAndUpdateBlock` (and other variants).

== Block Building Code Sample

[[block-preview]]
.Sample Block with Tabs (see <<block-sample1>>).
image::screenshot-dsl-block.webp[]

[source,groovy]
[[block-sample1]]
.Block Sample with Tabs
----
def editSlideshow(CmsPage slideshow) {
    UiBlockSpecifier b = new UiBlockSpecifier()

    Boolean createNew = slideshow == null
    if (!slideshow)
        slideshow = new CmsPage(pageType: CmsPageType.SLIDESHOW)

    b.ui {
        if (createNew)
            modal {
                ajaxBlock "createNew", {
                    form "Create Slideshow",
                            buildCmsSlideshowForm(slideshow),
                            BlockSpec.Width.MAX
                }
            }
        else {
            anonymousBlock BlockSpec.Width.HALF, {              <1>
                ajaxBlock "cmsSlideshowForm", {
                    form "Edit Slideshow",
                        buildCmsSlideshowForm(slideshow),
                        BlockSpec.Width.MAX
                }
            }

            blockTabs BlockSpec.Width.HALF, {
                blockTab "Images", buildImagesTab(slideshow)    <2>
                blockTab "Videos", buildVideosTab(slideshow)
            }
        }
    }
    taackUiService.show(b, buildMenu())                   <3>
}
----

<1> Declare an invisible block of 1/2 window width
<2> Declare 2 tabs inside, the first one is displayed when the page appear
<3> Send the bloc along with the menu description to the browser.

== Modal Stack Code Sample

After processing a form, you can refresh the content of the page, or another modal below the current one. You also can reload the page, or display another page. Reloading the page allows the code to be reusable, since it does not depend on the current URL (remember the `redirectAction` *params* you see in link:../../first-app.adoc#_adding_buttons_to_a_table_block[First App (Adding buttons to a table block)] .

Usually a modal is closed after processing a form. Either it is not valid, and errors are shown into the browser directly into the form, either we save the object and refresh the content.

See

=== Close Modal and reload the page

[source,groovy]
----
@Transactional
@Secured(["ROLE_CMS_MANAGER", "ROLE_CMS_DIRECTOR"])
def saveCmsInsert() {
    taackSimpleSaveService.saveThenReloadOrRenderErrors(CmsInsert)
}
----

If no error is detected saving CmsInsert, the page is simply reloaded.

=== Close Modal and load a page

[source,groovy]
----
@Transactional
@Secured(["ROLE_ADMIN", "ROLE_CMS_MANAGER", "ROLE_CMS_DIRECTOR"])
def saveCmsPage() {
    taackSimpleSaveService.saveThenRedirectOrRenderErrors(  <1>
        CmsPage, this.&pages as MethodClosure
    )
}
----
<1> The method closure in this controller will be loaded (respecting URL Mappings)

=== Close Modal and refresh page ajax blocks

.Complex Use Case: close the modal window and refresh ajaxBlock present in the page
[source,groovy]
----
taackSimpleSaveService.saveThenDisplayBlockOrRenderErrors(
    EngineeringChangeRequest,                                   <1>
    new UiBlockSpecifier().ui {                                 <2>
        closeModalAndUpdateBlock {                              <3>
            ajaxBlock ajaxBlockId, {                            <4>
                show "Projects", buildShowProjects(ecr),
                    BlockSpec.Width.MAX, {
                        action "Edit projects 2", ActionIcon.ADD,
                            Ecr2Controller.&projectsForm as MC,
                            [id: ecr.id, ajaxBlockId: ajaxBlockId], true
                }
            }
        }
    }
)
----

<1> After an action implying to save an object is called into a modal, you can close the modal and refresh page elements in one action
<2> `saveThenDisplayBlockOrRenderErrors` take a `UiBlockSpecifier` as parameter
<3> `closeModalAndUpdateBlock` will first close the last opened modal and then apply the modification
<4> Here, the block with the name contained in `ajaxBlockId` will be updated

== Block DSL Symbols Hierarchy

[graphviz,format="svg",align=center]
.Symbols hierachy diagram for Block DSL
----
digraph mygraph {
  node [shape=box];
  ui -> modal, closeModalAndUpdateBlock [label = "0,1"]
  closeModalAndUpdateBlock -> blockTabs, ajaxBlock [label = "1,N"]
  ui, anonymousBlock, modal -> anonymousBlock, ajaxBlock [label = "0,N"]
  ui, anonymousBlock, modal -> blockTabs [label = "0,1"]
  blockTabs -> ajaxBlockTab [label = "1,N"]
  ajaxBlock, ajaxBlockTab -> form, show, tableFilter, table, graphs, custom, anonymousBlock [label = "1,N"]
}
----

