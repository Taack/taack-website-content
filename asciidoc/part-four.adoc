= Graphs

This section explains how to generate data and implement different types of graphs in your library management application using Grails and the Taack UI framework.


== Set Up
.To access the skeleton for this part, go to your terminal cd into the project (`myLibrary`) and copy the following line.
[,bash]
----
git checkout part-4-skeleton
----

Or in IntelliJ you can select the wanted branch directly. Select in the top left corner main/myLibrary/remote, and you will see all the branches. Select a branch add checkout.

== Creating Data

We need data from our users to create graphs. The data is created using Grails domain classes and services to generate realistic entries for users, authors, books, and borrow records. For more information, refer to the xref:https://gorm.grails.org/latest/hibernate/manual/index.html#_data_service_queries[GORM Documentation].

You can also have a look at the newly added file located in `app/myLibrary/grails-app/controllers/my/library/MyLibraryDataController.groovy`.


To create the data, simply call `/myLibraryData/createDataUsers` in the address bar of your browser. Note this will not erase previously created data, and will not create the data again if the data was already created.


== Creating Graphs

=== Example Code

To create a graph, you will use a `UiDiagramSpecifier()`. There are a variety of different graph options (bar, line, pie, whiskers, area). To have a look at how to implement all of them, refer to link:doc/DSLs/diagram-dsl.adoc[Diagram DSL].

In this example, we want to visualize the number of books per author for the top 10 authors with the most books in the library.

First, we need to retrieve the information from MyLibraryBook.

.Hql code (example code):
[,Groovy]
----
List<Object[]> results = MyLibraryBorrowed.executeQuery("""\
    select book.author, count(book)
    from MyLibraryBook book
    group by book.author
     """, [max: 10])
----
Here we store each of the top 10 authors’ lastName with their associated number of books.

We now need to put this information in a readable format for the DiagramSpecifier.

[,Groovy]
.Example code:
----
Map<String, BigDecimal> authorCounts = [:]
results.each { row ->
    String lastName = row[0] as String
    Long count = row[1] as Long
    authorCounts[lastName] = count.toBigDecimal()
}
----

We can now create the pie diagram.

.Pie Diagram (example code):
[,Groovy]
----
new UiDiagramSpecifier().ui {
    pie(hasSlice, {
        List<String> authorsSorted = authorCounts.keySet().toList()
        List<BigDecimal> counts = authorsSorted.collect { authorCounts[it] }

        // For pie, each dataset entry is a separate slice with its label
        labels "Author Pie"
        (0..<authorsSorted.size()).each { i ->
            dataset(authorsSorted[i], counts[i])
        }
    })
}
----

This code has already been implemented for you in `MyLibraryUiService`, along with the Diagram menu. We have started the implementation of `listDiagram` for your convenience. Have a look at `MyLibraryController`.

You can run the server and navigate to the Diagram menu to see the example pie diagram.
Let's create our own diagrams.

=== Pie Diagram
We want to be able to visualize which books are the most popular among the borrowers. For that, we decided to create a pie graph displaying the top 10 books that are the most borrowed.

Pro Mode: Implement `buildBookPopularityPieDiagram` [TODO 1.1].

.Implement `buildBookPopularityPieDiagram`:
[,Groovy]
----
UiDiagramSpecifier buildBookPopularityPieDiagram(boolean hasSlice) {
    Map<String, BigDecimal> bookCounts = [:]

    List<Object[]> results = MyLibraryBorrowed.executeQuery("""\
        select bi.book.title, count(borrowed)
        from MyLibraryBorrowed borrowed
        join borrowed.bookInstance bi
        group by bi.book.title
        order by count(borrowed) desc
         """, [max: 10])

    results.each { row ->
        String title = row[0] as String
        Long count = row[1] as Long
        bookCounts[title] = count.toBigDecimal()
    }

    new UiDiagramSpecifier().ui {
        pie(hasSlice, {
            List<String> titlesSorted = bookCounts.keySet().toList()
            List<BigDecimal> counts = titlesSorted.collect { bookCounts[it] }

            labels "Pie"
            (0..<titlesSorted.size()).each { i ->
                dataset(titlesSorted[i], counts[i])
            }
        })
    }
}
----

Pro Mode: Render the diagram in `listDiagram` [TODO 1.2].

.Modify `listDiagram`, replace [TODO 1.2.1 and 1.2.2]:
[,Groovy]
----
UiDiagramSpecifier diagramPieSpec = myLibraryUiService.buildBookPopularityPieDiagram(true) // TODO 1.2.1

diagram diagramPieSpec, {label "Book Popularity Pie"} // TODO 1.2.2
----

You now run the server again and navigate to the Diagram menu to see the newly created pie diagram.

=== Bar Diagram
We want to visualize the number of books requested by the borrowers. For that, we decided to create a bar graph displaying the number of requests (per year, month and day).

Pro Mode: Implement `buildBarDiagram` [TODO 2.1].
.Pro Mode helper (example code):
[,Groovy]
----
UiDiagramSpecifier barDiagram(boolean isStacked) {
    new UiDiagramSpecifier().ui {
        bar(isStacked, {
            labels "T1", "T2", "T3", "T4"
            dataset 'Truc1', 1.0, 2.0, 1.0, 4.0
            dataset 'Truc2', 2.0, 0.1, 1.0, 0.0
            dataset 'Truc3', 2.0, 0.1, 1.0, 1.0
        })
    }
}
----

.Implement `buildBarDiagram`:
[,Groovy]
----
UiDiagramSpecifier buildBarDiagram(boolean isStacked, String labelDateFormat) {
    Map<Date, BigDecimal> borrowedCounts = [:]

    List<Object[]> results1 = MyLibraryBorrowed.executeQuery("""\
        select count(borrowed), borrowed.requestDate
        from MyLibraryBorrowed borrowed
        group by borrowed.requestDate
        """)

    results1.each { row ->
        Long count = row[0] as Long
        Date date = row[1] as Date
        borrowedCounts[date] = (borrowedCounts[date] ?: 0) + count.toBigDecimal()
    }

    new UiDiagramSpecifier().ui {
        bar(isStacked, {
            List<Map.Entry<Date, BigDecimal>> countsSorted = borrowedCounts.entrySet().sort { it.key }.collect { it }

            labels((labelDateFormat ?: 'YEAR') as DiagramXLabelDateFormat, countsSorted*.key as Date[]) //<1>
            dataset('Borrowed Books', countsSorted*.value as BigDecimal[])
        })
    }
}
----
<1> By default, the graph will display per year.

Pro Mode: Render the diagram in `listDiagram` [TODO 2.2].

.Modify `listDiagram`, replace [TODO 2.2.1 and 2.2.2]:
[,Groovy]
----
UiDiagramSpecifier diagramBarSpec = myLibraryUiService.buildBarDiagram(true, labelDateFormat) // TODO 2.2.1

// TODO 2.2.2
diagram(diagramBarSpec, {
    menu 'Yearly', this.&listDiagrams as MethodClosure, [labelDateFormat: 'YEAR']
    menu 'Monthly', this.&listDiagrams as MethodClosure, [labelDateFormat: 'MONTH']
    menu 'Daily', this.&listDiagrams as MethodClosure, [labelDateFormat: 'DAY']
})
----

You now run the server again to see the newly created bar diagram.


=== Whiskers Diagram

We want to be able to visualize the number of days a book is rented for by the borrowers. To achieve this, we decided to create a whisker graph displaying the number of books rented within each specified range of days (1–3, 4–7, 8–14, 15–20, and more than 21).

Pro Mode: Implement `buildBorrowDurationWhiskersDiagram` [TODO 3.1].
.Pro Mode helper (example code):
[,Groovy]
----
diagram new UiDiagramSpecifier().ui({
    whiskers {
        labels "T1", "T2", "T3", "T4"

        dataset 'Truc1', {
            boxData 1.0, 2.0, 3.0, 3.5, 4.0, 4.5, 5.0
            boxData 1.5, 2.5, 3.5, 6.0, 7.0, 8.0, 9.0
            boxData 2.0, 2.0, 2.1, 2.5, 5.5, 5.6, 6.7
            boxData 1.7, 1.8, 1.9, 2.0, 2.1, 2.2, 2.3
        }
    }
})

----

.Implement `buildBorrowDurationWhiskersDiagram`:
[,Groovy]
----
UiDiagramSpecifier buildBorrowDurationWhiskersDiagram() {
    Map<Integer, BigDecimal> borrowedDates = [:]
    Map<String, List<BigDecimal>> bins = [
            "1-3d"  : [],
            "4-7d"  : [],
            "8-14d" : [],
            "15-20d": [],
            "21+d"  : []
    ]

    List<Object[]> results1 = MyLibraryBorrowed.executeQuery("""\
        select borrowed.approvalDate, borrowed.returnDate
        from MyLibraryBorrowed borrowed
        where borrowed.approvalDate is not null and borrowed.returnDate is not null
""")

    results1.each { row ->
        Date approvalDate = row[0] as Date
        Date returnDate = row[1] as Date

        long durationMillis = returnDate.time - approvalDate.time
        int duration = (durationMillis / (1000 * 60 * 60 * 24)).toInteger()
        borrowedDates[duration] = (borrowedDates[duration] ?: 0.toBigDecimal()) + 1.toBigDecimal()
        BigDecimal durationBD = duration as BigDecimal

        if (duration <= 3) bins["1-3d"] << durationBD
        else if (duration <= 7) bins["4-7d"] << durationBD
        else if (duration <= 14) bins["8-14d"] << durationBD
        else if (duration <= 20) bins["15-20d"] << durationBD
        else bins["21+d"] << durationBD
    }

    new UiDiagramSpecifier().ui {
        whiskers {
            labels bins.keySet() as String[]

            dataset('Borrow Durations', {
                bins.each { label, values ->
                    if (values) {
                        List<BigDecimal> sorted = values.sort()
                        // min, Q1, median, Q3, max + dummy outliers if needed
                        boxData sorted[0], sorted[(int)(sorted.size()*0.25)], sorted[(int)(sorted.size()*0.5)],
                                sorted[(int)(sorted.size()*0.75)], sorted[-1],
                                sorted[0], sorted[-1]
                    } else {
                        boxData 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0
                    }
                }
            })
        }
    }
}

----

Pro Mode: Render the diagram in `listDiagram` [TODO 3.2].

.Modify `listDiagram`, replace [TODO 3.2.1 and 3.2.2]:
[,Groovy]
----
UiDiagramSpecifier durationDiagramSpec = myLibraryUiService.buildBorrowDurationWhiskersDiagram() // TODO 3.2.1

diagram durationDiagramSpec // TODO 3.2.2
----

You now run the server again to see the newly created whiskers diagram.
